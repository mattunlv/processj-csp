include "alt.csp"
include "alt-spec.csp"
include "scheduler.csp"

-- Set of ProcessJ processes.
Processes = { 0..2 }

-- Define actual processes
NULL = -1
W0 = 0
W1 = 1
R = 2

-- Set of ProcessJ channels
Channels = { 0..1 }

-- Set of nullable ProcessJ processes
Nullable_Processes = union({ NULL }, Processes)

-- Data values. 0 and 1 are distinct different values.
Values = { 0, 1 }

-- VARIABLES

PROTECTED_CHANNELS = ||| c : Channels @ (CHANNEL(c) ||| MONITOR(c))

alphaPROTECTED_CHANNELS = 
    union(
        Union({alphaCHANNEL(c) | c <- Channels}),
		Union({alphaMONITOR(c) | c <- Channels}))

-- PROTECTED WRITING PROCESS

PROTECTED_PROCESS_WRITER(pid, chan) = run.pid -> PROTECTED_PROCESS_WRITER'(pid, chan)

PROTECTED_PROCESS_WRITER'(pid, chan) =
    write.chan.pid?message ->
    claim.chan!pid ->
    WRITE(pid, chan, message);
    release.chan.pid ->
    YIELD(pid);
    ack.chan.pid ->
    PROTECTED_PROCESS_WRITER'(pid, chan)

alphaPROTECTED_PROCESS_WRITER(pid, chan) = 
    union(alphaWRITE(pid, chan), 
        {
            write.chan.pid.v,
            ack.chan.pid, 
            yield.pid, 
            run.pid,
            claim.chan.pid,
            release.chan.pid
            | v <- Values
        })

PROTECTED_PROCESS_SELECTOR(pid) =
    run.pid ->
    PROTECTED_PROCESS_SELECTOR'(pid)

PROTECTED_PROCESS_SELECTOR'(pid) =
    ready.pid.set!false ->
    select.pid ->
    PROTECTED_ENABLE(pid);
    YIELD(pid);
    PROTECTED_DISABLE(pid);
    selected.get?idx ->
    start_read.idx.pid ->
    claim.idx.pid ->
    READ(idx);
    selected.get?idx ->
    data.idx.get?message ->
    release.idx!pid ->
    read.idx.pid!message ->
    PROTECTED_PROCESS_SELECTOR'(pid)

alphaPROTECTED_PROCESS_SELECTOR(pid) = Union(
{
    alphaPROTECTED_ENABLE(pid),
    alphaPROTECTED_DISABLE(pid),
    Union({ alphaREAD(c) | c <- Channels }),
    {
        run.pid,
        yield.pid,
        select.pid,
        ready.pid.set.false,
        selected.get.c,
        start_read.c.pid,
        read.c.pid.v,
        claim.c.pid,
        release.c.pid
        | c <- Channels, v <- Values
    }
})

PROTECTED_ALT_SYSTEM = 
(
    (PROTECTED_CHANNELS ||| ALT_VARIABLES)
    [| union(diff(alphaPROTECTED_CHANNELS, { ready.p.get.b | p <- Processes, b <- Bool }), alphaALT_VARIABLES) |]
    (
        (PROTECTED_PROCESS_WRITER(W0, 0) ||| PROTECTED_PROCESS_WRITER(W1, 1) ||| PROTECTED_PROCESS_SELECTOR(R))
--        [|union(alphaMONITOR(0), alphaMONITOR(1))|]
--        (MONITOR(0) ||| MONITOR(1))
    )
) \ union(alphaPROTECTED_CHANNELS, alphaALT_VARIABLES)

PROTECTED_SCHEDULED_ALT_SYSTEM(N, schedule) = 
(
    PROTECTED_ALT_SYSTEM
    [| alphaN_SCHEDULER_SYSTEM |]
    N_SCHEDULER_SYSTEM(N, schedule)
) \ alphaN_SCHEDULER_SYSTEM

-- Deadlock checks
-- Single scheduler protected alt -- both failures and divergences
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 1, 2>) :[deadlock free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 2, 1>) :[deadlock free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 0, 2>) :[deadlock free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 2, 0>) :[deadlock free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 0, 1>) :[deadlock free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 1, 0>) :[deadlock free[FD]]
-- Two scheduler protected alt -- only failures, scheduler is divergent
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <0, 1, 2>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <0, 2, 1>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <1, 0, 2>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <1, 2, 0>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <2, 0, 1>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(2, <2, 1, 0>) :[deadlock free[F]]
-- Four scheduler protected alt -- only failures, scheduler is divergent 
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 1, 2>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 2, 1>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 0, 2>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 2, 0>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 0, 1>) :[deadlock free[F]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 1, 0>) :[deadlock free[F]]

-- Determinism checks
-- Single scheduler protected alt -- both failures and divergences
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 1, 2>) :[deterministic[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 2, 1>) :[deterministic[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 0, 2>) :[deterministic[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 2, 0>) :[deterministic[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 0, 1>) :[deterministic[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 1, 0>) :[deterministic[FD]]

-- Divergence checks
-- Only single scheduler
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 1, 2>) :[divergence free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 2, 1>) :[divergence free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 0, 2>) :[divergence free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 2, 0>) :[divergence free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 0, 1>) :[divergence free[FD]]
assert PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 1, 0>) :[divergence free[FD]]

-- Refinement checks
-- Specification for one scheduler system - will only work in traces as single scheduler
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 1, 2>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <0, 2, 1>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 0, 2>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <1, 2, 0>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 0, 1>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(1, <2, 1, 0>)
-- Specification for two scheduler system - will also only work in traces
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <0, 1, 2>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <0, 2, 1>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <1, 0, 2>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <1, 2, 0>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <2, 0, 1>)
assert ALT_SPEC_SYSTEM(R) [T= PROTECTED_SCHEDULED_ALT_SYSTEM(2, <2, 1, 0>)
-- Four scheduler will work in failures but not divergences as scheduler is divergent
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 1, 2>)
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 2, 1>)
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 0, 2>)
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 2, 0>)
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 0, 1>)
assert ALT_SPEC_SYSTEM(R) [F= PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 1, 0>)
-- Four scheduler should now failures refine specification
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 1, 2>) [T= ALT_SPEC_SYSTEM(R)
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <0, 2, 1>) [T= ALT_SPEC_SYSTEM(R)
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 0, 2>) [T= ALT_SPEC_SYSTEM(R)
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <1, 2, 0>) [T= ALT_SPEC_SYSTEM(R)
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 0, 1>) [T= ALT_SPEC_SYSTEM(R)
assert PROTECTED_SCHEDULED_ALT_SYSTEM(4, <2, 1, 0>) [T= ALT_SPEC_SYSTEM(R)

print card(Events)
print Events